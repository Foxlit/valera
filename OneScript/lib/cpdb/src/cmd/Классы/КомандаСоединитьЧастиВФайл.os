// ----------------------------------------------------------
// This Source Code Form is subject to the terms of the
// Mozilla Public License, v.2.0. If a copy of the MPL
// was not distributed with this file, You can obtain one
// at http://mozilla.org/MPL/2.0/.
// ----------------------------------------------------------
// Codebase: https://github.com/ArKuznetsov/cpdb/
// ----------------------------------------------------------

#Использовать "../../core"

#Область СлужебныйПрограммныйИнтерфейс

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
	
	Команда.Опция("pp params", "", "Файлы JSON содержащие значения параметров,
	                               | могут быть указаны несколько файлов разделенные "";""")
	       .ТСтрока()
	       .ВОкружении("CPDB_PARAMS");

	Команда.Опция("a arch file", "", "имя первого файла архива")
	       .ТСтрока()
	       .ВОкружении("CPDB_FILE_MERGE_ARCH");
	
	Команда.Опция("l list", "", "имя файла, списка томов архива")
	       .ТСтрока()
	       .ВОкружении("CPDB_FILE_MERGE_LIST");
	
	Команда.Опция("ds delsrc", Ложь, "удалить исходные файлы после выполнения операции")
	       .Флаговый()
	       .ВОкружении("CPDB_FILE_MERGE_DEL_SRC");
	
КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	ЧтениеОпций = Новый ЧтениеОпцийКоманды(Команда);

	ВыводОтладочнойИнформации = ЧтениеОпций.ЗначениеОпции("verbose");

	ПараметрыСистемы.УстановитьРежимОтладки(ВыводОтладочнойИнформации);

	ЭтоСписокФайлов = Истина;

	ПутьКАрхиву = ЧтениеОпций.ЗначениеОпции("list");
	Если НЕ ЗначениеЗаполнено(ПутьКАрхиву) Тогда
		ПутьКАрхиву = ЧтениеОпций.ЗначениеОпции("arch");
		ЭтоСписокФайлов = Ложь;
	КонецЕсли;
	УдалитьИсточник = ЧтениеОпций.ЗначениеОпции("delsrc");
	
	Если ПустаяСтрока(ПутьКАрхиву) Тогда
		ВызватьИсключение "Не указан путь к первому файлу архива или списку файлов";
	КонецЕсли;
	
	РаботаСФайлами.РаспаковатьАрхив(ПутьКАрхиву, ЭтоСписокФайлов, УдалитьИсточник);

КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // СлужебныйПрограммныйИнтерфейс
